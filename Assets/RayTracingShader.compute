// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

static const float PI = 3.14159265f;
static const float epsilon = 1E-6;
RWTexture2D<float4> Result;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
uint _MaxBounces;
uint _Samples;
uint _FrameCount;
float _RescaleFactor;
float2 _PixelOffset;

float RandomValue(inout uint rngState)
{
    rngState = rngState * 747796405 + 2891336453;
    uint result = ((rngState >> ((rngState >> 28) + 4)) ^ rngState) * 277803737;
    result = (result >> 22) ^ result;
    return result / 4294967295.0;
}

float RandomValueNormalDistribution(inout uint rngState)
{
    float theta = 2 * PI * RandomValue(rngState);
    float rho = sqrt(-2 * log(RandomValue(rngState)));
    return rho * cos(theta);
}

float3 RandomDirection(inout uint rngState)
{
    float x = RandomValueNormalDistribution(rngState);
    float y = RandomValueNormalDistribution(rngState);
    float z = RandomValueNormalDistribution(rngState);
    return normalize(float3(x,y,z));
}

struct Ray
{
    float3 origin;
    float3 direction;
};

Ray CameraRay(float2 uv)
{
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    // Transform the direction from camera to world space and normalize
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

struct HitInfo
{
    bool didHit;
    float3 hitPoint, normal;
    float distance;
    
    float4 diffuse;
    float4 emission;
};

struct Triangle
{
    float3 p1, p2, p3;
    float3 n1, n2, n3;
};

struct MeshObject
{
    float4x4 localToWorld;
    int firstIndex;
    int indexCount;
    float3 boundsMin;
    float3 boundsMax;
    
    float4 diffuseColor;
    float4 emissiveColor;
    float emission;
};

StructuredBuffer<MeshObject> _meshes;
StructuredBuffer<float3> _vertices;
StructuredBuffer<float3> _normals;
StructuredBuffer<int> _indices;

bool RayBoundingBox(Ray ray, float3 boundsMin, float3 boundsMax)
{
    //float3 boundsCenter = (boundsMin + boundsMax) / 2;
    float3 tmin;
    float3 tmax;
    
    float3 invDir = 1 / ray.direction;
    int3 sign;
    sign.x = invDir.x >= 0 ? 0 : 1;
    sign.y = invDir.y >= 0 ? 0 : 1;
    sign.z = invDir.z >= 0 ? 0 : 1;

    float2x3 bounds = float2x3(boundsMin, boundsMax);
    tmin.x = (bounds[sign.x].x - ray.origin.x) * invDir.x;
    tmax.x = (bounds[1 - sign.x].x - ray.origin.x) * invDir.x;
    tmin.y = (bounds[sign.y].y - ray.origin.y) * invDir.y;
    tmax.y = (bounds[1 - sign.y].y - ray.origin.y) * invDir.y;

    if ((tmin.x > tmax.y) || (tmin.y > tmax.x))
        return false;
    
    if (tmin.y > tmin.x)
        tmin.x = tmin.y;
    if (tmax.y < tmax.x)
        tmax.x = tmax.y;

    tmin.z = (bounds[sign.z].z - ray.origin.z) * invDir.z;
    tmax.z = (bounds[1 - sign.z].z - ray.origin.z) * invDir.z;

    if ((tmin.x > tmax.z) || (tmin.z > tmax.x))
        return false;

    /*if (tmin.z > tmin.x)
        tmin.x = tmin.z;
    if (tmax.z < tmax.x)
        tmax.x = tmax.z;*/
    
    return true; 
}

HitInfo RayTriangle(Ray ray, Triangle tri)
{
    HitInfo hitInfo = (HitInfo)0;
    hitInfo.distance = 1.#INF;
    const float epsilon = 1E-6;
    float3 edge1, edge2, h, s, q;
    float a, f, u, v;
    
    edge1 = tri.p2 - tri.p1;
    edge2 = tri.p3 - tri.p1;

    h = cross(ray.direction, edge2);
    a = dot(edge1, h);

    if(a > -epsilon && a < epsilon)
        return hitInfo;

    f = 1.0 / a;
    s = ray.origin - tri.p1;
    u = f * dot(s, h);

    if(u < 0 || u > 1)
        return hitInfo;

    q = cross(s, edge1);
    v = f * dot(ray.direction, q);

    if(v < 0 || u + v > 1)
        return hitInfo;

    float dst = f * dot(edge2, q);

    if(dst > epsilon)
    {
        hitInfo.didHit = true;
        hitInfo.hitPoint = ray.origin + ray.direction * dst;
        hitInfo.distance = dst;
        //hitInfo.normal = tri.p1 + tri.p2 + tri.p3;
        hitInfo.normal = normalize(cross(tri.p2 - tri.p1, tri.p3 - tri.p1));
    }
    return hitInfo;
}

HitInfo RaySphere(Ray ray, float3 sphereCenter, float sphereRadius)
{
    HitInfo hitInfo = (HitInfo)0;
    float3 offsetRayOrigin = ray.origin - sphereCenter;
    float a = 1;
    float b = 2 * dot(offsetRayOrigin, ray.direction);
    float c = dot(offsetRayOrigin, offsetRayOrigin) - sphereRadius * sphereRadius;

    float discriminant = b * b - 4 * a * c;

    if(discriminant >= 0)
    {
        float dst = (-b - sqrt(discriminant)) / (2 * a);

        if(dst >= 0)
        {
            hitInfo.didHit = true;
            hitInfo.distance = dst;
            hitInfo.hitPoint = ray.origin + ray.direction * dst;
            hitInfo.normal = normalize(hitInfo.hitPoint - sphereCenter);
        }
    }

    return hitInfo;
}

HitInfo RayMesh(Ray ray, MeshObject mesh)
{
    HitInfo closestHit = (HitInfo)0;
    closestHit.distance = 1.#INF;
    
    if(!RayBoundingBox(ray, mesh.boundsMin, mesh.boundsMax))
        return closestHit;
    
    
    uint offset = mesh.firstIndex;
    uint count = offset + mesh.indexCount;

    Triangle tri;
    for (uint i = offset; i < count; i += 3)
    {
        tri.p1 = mul(mesh.localToWorld, float4(_vertices[_indices[i]], 1)).xyz;
        tri.p2 = mul(mesh.localToWorld, float4(_vertices[_indices[i + 1]], 1)).xyz;
        tri.p3 = mul(mesh.localToWorld, float4(_vertices[_indices[i + 2]], 1)).xyz;

        tri.n1 = mul(mesh.localToWorld, float4(_normals[_indices[i]], 1)).xyz;
        tri.n2 = mul(mesh.localToWorld, float4(_normals[_indices[i + 1]], 1)).xyz;
        tri.n3 = mul(mesh.localToWorld, float4(_normals[_indices[i + 2]], 1)).xyz;

        HitInfo hitInfo = RayTriangle(ray, tri);

        if (hitInfo.didHit)
        {
            if (hitInfo.distance > epsilon && hitInfo.distance < closestHit.distance)
            {
                closestHit = hitInfo;
                closestHit.diffuse = mesh.diffuseColor;
                closestHit.emission = mesh.emissiveColor * mesh.emission;
            }
        }
    }
    return closestHit;
}

HitInfo RayAll(Ray ray)
{
    HitInfo closestHit = (HitInfo)0;
    closestHit.distance = 1.#INF;
    uint count, stride;
    _meshes.GetDimensions(count, stride);
    
    for (uint i = 0; i < count; i++)
    {
        HitInfo hitInfo = RayMesh(ray, _meshes[i]);
        
        if(hitInfo.distance < closestHit.distance)
            closestHit = hitInfo;
    }
    
    return closestHit;
}

float3 Trace(Ray ray, out float3 normal, inout uint rngState)
{
    float3 incomingLight = 0;
    float3 rayColor = 1;
    
    for(uint i = 0; i <= _MaxBounces; i++)
    {
        HitInfo hit = RayAll(ray);

        if(i == 0) normal = hit.normal; 

        if(hit.didHit)
        {
            float decreaseByAngle = abs(dot(hit.normal, normal));
            float3 direction = normalize(hit.normal + RandomDirection(rngState));
        
            ray.origin = hit.hitPoint;
            ray.direction = direction;
            incomingLight += hit.emission.rgb * rayColor * decreaseByAngle;
            rayColor = rayColor * hit.diffuse.rgb;
        }
        else break;
    }

    return incomingLight;
}

[numthreads(16,9,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint width, height;
    Result.GetDimensions(width, height);
    float2 uv = float2(((id.xy + _PixelOffset)) / (float2(width, height)) * 2 - 1);
    //uv = uv - _RescaleFactor + 1;

    uint2 numPixels = uint2(width, height);
    uint2 pixelCoord = (uv + 1) * numPixels;
    uint pixelIndex = pixelCoord.x + pixelCoord.y * numPixels.x;
    uint rngState = pixelIndex + _FrameCount * 747796405;

    Ray ray = CameraRay(uv);

    float3 totalIncomingLight = 0;
    float3 normal;
    for(uint rayIndex = 0; rayIndex < _Samples; rayIndex++)
    {
        totalIncomingLight += Trace(ray, normal, rngState);
    }
    
    float4 output = float4(totalIncomingLight / _Samples, 1);
    
    Result[id.xy] = output;
    //Result[id.xy] = float4(normal, 1);
}
